extends Node

@export var generator : DailyLetterSetGenerator
@export var tileHolder : TileDock
@export var slots : Array[DropSlot]
@export var tiles : Array[DropTile]
@export var wordScene : InstancePlaceholder
@export var score : Label

var lineSlotIndexes = [
	[0,  1,  2,  3,  4], 
	[5,  6,  7,  8,  9], 
	[10, 11, 12, 13, 14], 
	[15, 16, 17, 18, 19], 
	[20, 21, 22, 23, 24], 
	
	[0, 5, 10, 15, 20], 
	[1, 6, 11, 16, 21], 
	[2, 7, 12, 17, 22], 
	[3, 8, 13, 18, 23], 
	[4, 9, 14, 19, 24], 
	
	[0, 6, 12, 18, 24], 
	[4, 8, 12, 16, 20],
	
	[1, 7, 13, 19],
	[3, 7, 11, 15],
	[5, 11, 17, 23],
	[9, 13, 17, 21],
	
	[2, 6, 10],
	[2, 8, 14],
	[10, 16, 22],
	[14, 18, 22]
]

var test_letter_set = ["s","p","a","n","s","m","n","r","a","t","a","u","i","o","o","r","n","l","p","p","t","r","a","p","s"]

var valid_words = []
var wordInstanceMap = {}

# Called when the node enters the scene tree for the first time.
func _ready() -> void:
	generator.generate(25)
	
	generator.generated_set.shuffle()
	var i = 0
	for tile in tiles:
		tile.set_letter(generator.generated_set[i])
		i += 1
		tileHolder.setup_tile(tile)
	
	for slot in slots:
		slot.tile_changed.connect(update)
	
	var file = FileAccess.open("res://small.txt", FileAccess.READ)
	while (file.get_position() <file.get_length()):
		var line = file.get_line()
		valid_words.append(line)
	file.close()

var dash = "-"

func update(slot: DropSlot):
	var words = {}
	for line in lineSlotIndexes:
		#print(str(line))
		var l2 = line[2]
		#if middle slot empty, no words possible in this line so can early exit this check
		var c = slots[l2].letter()
		if (c == dash):
			continue
			
		var l0 = line[0]
		var a = slots[l0].letter()
		var l1 = line[1]
		var b = slots[l1].letter()
		
		var chunk : String = a+b+c
		check_chunk(chunk, [l0,l1,l2], words)
		
		if (line.size() < 4):
			continue
		
		var l3 = line[3]
		var d = slots[l3].letter()
		if (d == dash):
			continue
		
		chunk = b+c+d
		check_chunk(chunk, [l1,l2,l3], words)
		
		chunk = a+b+c+d
		check_chunk(chunk, [l0,l1,l2,l3], words)
		
		if (line.size() < 5):
			continue
		
		var l4 = line[4]
		var e = slots[l4].letter()
		if (e == dash):
			continue
		
		chunk = c+d+e
		check_chunk(chunk, [l2,l3,l4], words)
		if (b == dash):
			continue
		
		chunk = b+c+d+e
		check_chunk(chunk, [l1,l2,l3,l4], words)
		if (a == dash):
			continue
		
		chunk = a+b+c+d+e
		check_chunk(chunk, [l0,l1,l2,l3,l4], words)
	
	for wordInstance in wordInstanceMap:
		if !words.has(wordInstance):
			wordInstanceMap[wordInstance].queue_free()
			wordInstanceMap.erase(wordInstance)
	
	for word in words:
		make_word(word, words[word])
	
	var total = 0
	for word in wordInstanceMap:
		total += wordInstanceMap[word].get_points()
	
	score.text = "SCORE: " + str(total)

func check_chunk(chunk: String, indexes: Array[int], words: Dictionary):
	#print(chunk)
	if (chunk.contains(dash)):
		return
	if (valid_words.has(chunk)):
		words[chunk] = indexes
		#print("----yay that's a word!")
	chunk = chunk.reverse()
	if (valid_words.has(chunk)):
		words[chunk] = indexes
		#print("----yay that's a word!")

func make_word(word: String, indexes: Array[int]):
	if word in wordInstanceMap:
		return
	var wordInstance : Word = wordScene.create_instance()
	wordInstance.set_word(word, indexes, self)
	wordInstanceMap[word] = wordInstance

func highlight(indexes):
	for index in indexes:
		slots[index].highlight()
		
func reset_tiles():
	for slot in slots:
		if slot.slotTile == null:
			continue
		var tile = slot.slotTile
		tile.dragged_away.emit(tile)
		tileHolder.add_tile(tile)
